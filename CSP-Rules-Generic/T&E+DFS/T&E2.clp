
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;                              CSP-RULES / GENERIC
;;;                              TRIAL & ERROR, DEPTH 2
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



               ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
               ;;;                                                    ;;;
               ;;;              copyright Denis Berthier              ;;;
               ;;;     https://denis-berthier.pagesperso-orange.fr    ;;;
               ;;;             January 2006 - April 2021              ;;;
               ;;;                                                    ;;;
               ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



; -*- clips -*-






;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; CONTEXT INITIALISATION
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-init-non-first-context-c-values
	"copy all the c-values from the parent context"
	(declare (salience ?*init-context-salience*))
    (technique 0 TE2)
	(context (name ?cont&~0) (parent ?par))
	(technique ?cont BRT)
	(not (clean-and-retract ?cont))
	?cv <- (candidate (status c-value) (context ?par))
=>
	;;; copied c-values are created with flag 0, 
    ;;; because all the incompatible candidates have been eliminated
    (duplicate ?cv (context ?cont) (flag 0))
)


(defrule TE2-init-non-first-context-candidates
	"copy all the candidates from the parent context, except those linked to the generating c-value"
	(declare (salience ?*init-context-salience*))
    (technique 0 TE2)
	(context (name ?cont&~0) (parent ?par) (generating-cand ?gen-cand))
	(technique ?cont BRT)
	(not (clean-and-retract ?cont))
	?cand <- (candidate (context ?par) (status cand) (label ?xxx&~?gen-cand&:(not (labels-linked ?xxx ?gen-cand))))
=>
	(duplicate ?cand (context ?cont))
)




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; CONTEXT CLEANING
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-dummy-rule
    (init-context 0)
    (technique 0)
    (csp-linked 0)
    (exists-link 0)
=>
)


(defrule TE2-clean-context
    (declare (salience ?*clean-context-salience*))
    (technique 0 TE2)
    (context (name ?cont&~0))
    (clean-and-retract ?cont)
=>
    (do-for-all-facts
        ((?f candidate))
        (eq ?f:context ?cont)
        (retract ?f)
    )
    (do-for-all-facts
        ((?f chain))
        (eq ?f:context ?cont)
        (retract ?f)
    )
    (do-for-all-facts
        ((?f init-context))
        (eq (nth$ 1 ?f:implied) ?cont)
        (retract ?f)
    )
    (do-for-all-facts
        ((?f technique))
        (eq (nth$ 1 ?f:implied) ?cont)
        (retract ?f)
    )
    (do-for-all-facts
        ((?f csp-linked))
        (eq (nth$ 1 ?f:implied) ?cont)
        (retract ?f)
    )
    (do-for-all-facts
        ((?f exists-link))
        (eq (nth$ 1 ?f:implied) ?cont)
        (retract ?f)
    )
)




(defrule TE2-clean-context-end
    (declare (salience ?*clean-context-salience*))
    (technique 0 TE2)
    ?ctx <- (context (name ?cont&~0) (parent ?par))
    ?rr <- (clean-and-retract ?cont)
    (not (init-context ?cont))
    (not (candidate (context ?cont)))
    (not (csp-linked ?cont $?))
    (not (exists-link ?cont $?))
    (not (chain (context ?cont)))
=>
    (retract ?rr)
    (retract ?ctx)
    (if (or ?*print-actions* ?*print-hypothesis*) then
        (printout t "BACK IN CONTEXT " ?par " with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining." crlf crlf)
    )
)




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; DETECTION OF CONTRADICTION IN CONTEXT 
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-detect-contradiction-in-non-first-context
	(declare (salience ?*contradiction-in-context-salience*))
	;;; in a context generated by ?gen-cand, which is not the first
    (technique 0 TE2)
	(context (name ?cont&~0) (parent ?par) (generating-cand ?gen-cand))
	?g <- (candidate (context ?par) (status cand) (label ?gen-cand))
	;;; if there is a csp-variable with no c-value and no candidate in ?cont
	(csp-variable (name ?csp)) ; (type rc)
	(forall (is-csp-variable-for-label (csp-var ?csp) (label ?lab))
        (not (candidate (context ?cont) (label ?lab)))
    )
	(phase ?par ?ph)
	(not (clean-and-retract ?cont))
=>
	;;; then eliminate the generating candidate from the parent context
	(retract ?g)
	(if (eq ?par 0) then (bind ?*nb-candidates* (- ?*nb-candidates* 1)))
	(if (or ?*print-actions* ?*print-hypothesis*) then
		(printout t "NO POSSIBLE VALUE for csp-variable " ?csp " IN CONTEXT " ?cont ".")
		(printout t " RETRACTING CANDIDATE " (print-label ?gen-cand) " FROM CONTEXT " ?par "." crlf crlf)
	)
	;;; remember that this phase was productive
	(assert (phase-productive-in-context ?par ?ph))
	;;; properly destroy the present context so as not to saturate memory
	(assert (clean-and-retract ?cont))
)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; DETECTION OF NO CONTRADICTION, CONTEXT GENERATION AND PHASE ITERATION IN A CONTEXT 
;;; - level 2
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-no-contradiction-found-in-context-level2
	(declare (salience ?*level2-no-contrad-found-in-context-salience*))
	;;; after all the resolution rules have been applied in the context
	(context (name ?cont&~0) (depth ?depth&2) (parent ?par) (generating-cand ?gen-cand))
=>
	(if (or ?*print-actions* ?*print-hypothesis*) then
		(printout t "NO CONTRADICTION FOUND IN CONTEXT " ?cont "." crlf)
	)
	;;; properly destroy the present context so as not to saturate memory
	(assert (clean-and-retract ?cont))
)


(defrule TE2-start-TE-level2
	(declare (salience ?*level2-generate-context-salience*))
    (technique 0 TE2)
	(context (name ?cont) (depth ?depth&1))
=>
	(assert (phase ?cont 1))
	(printout t crlf "*** STARTING T&E IN CONTEXT " ?cont  " at depth " ?depth " with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining ***" crlf)
	(if ?*print-phase* then
        (if ?*print-hypothesis* then (printout t crlf))
        (printout t "        STARTING PHASE 1 IN CONTEXT " ?cont " AT DEPTH 1, with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining" crlf)
        (if ?*print-hypothesis* then (printout t crlf))
    )
)



(defrule TE2-generate-context-level2
	(declare (salience ?*level2-generate-context-salience*))
    (technique 0 TE2)
    ;;; level 2
	(context (name ?par) (depth ?depth&1))
    ;;; only one context with parent ?par will be considered at a time:
	(not (context (name ?cont&~?par) (parent ?par)))
	;;; each remaining cand of ?par will be re-tried in each phase, but not re-tried in the same phase
	(phase ?par ?ph) 
    ?gen <- (candidate (context ?par) (status cand) (label ?gen-cand))
	(not (TE2-tried ?par ?ph ?gen-cand))
    ;;; try only the candidates that are restricted targets for T&E
    (or (test (not ?*restrict-TE-targets*))
        (is-restricted-TE-target 0 ?gen-cand)
    )
=>
	;;; choose ?gen-cand as a hypothesis	
	(bind ?*context-counter* (+ ?*context-counter* 1))
	(bind ?depth1 (+ 1 ?depth))
	(if (or ?*print-actions* ?*print-hypothesis*) then 
		(printout t crlf "GENERATING CONTEXT " ?*context-counter* " AT DEPTH " ?depth1 ", SON OF CONTEXT " ?par ", FROM HYPOTHESIS " ?gen-cand "." crlf)
	)
	;;; assert the new context
	(assert (context (name ?*context-counter*) (parent ?par) (depth ?depth1) (generating-cand ?gen-cand)))
	(assert (technique ?*context-counter* BRT))
	;;; assert the generating value of the new context, with flag 0
	(duplicate ?gen (context ?*context-counter*) (status c-value) (flag 0))
	;;; remember that ?gen-cand was tried in ?par at phase ?ph
	(assert (TE2-tried ?par ?ph ?gen-cand))
)



(defrule TE2-iterate-phase-level2
	(declare (salience ?*level2-iterate-phase-salience*))
    (technique 0 TE2)
	;;; if the grid is not solved by phase ?ph
	(context (name ?par) (depth ?depth&1))
	(csp-variable (name ?csp))
	(forall (is-csp-variable-for-label (csp-var ?csp) (label ?lab))
        (not (candidate (context ?par) (status c-value) (label ?lab)))
    )
	?phase <- (phase ?par ?ph)
	;;; and if phase ?ph was productive
	?ce <- (phase-productive-in-context ?par ?ph)
	
=>
	(if ?*print-phase* then
        (if ?*print-hypothesis* then (printout t crlf))
        (printout t "        STARTING PHASE " (+ ?ph 1) " IN CONTEXT " ?par " AT DEPTH " ?depth " with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining" crlf)
        (if ?*print-hypothesis* then (printout t crlf))
    )
	(assert (phase ?par (+ ?ph 1)))
	(retract ?ce)
	(retract ?phase)
)




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; DETECTION OF NO CONTRADICTION, CONTEXT GENERATION AND PHASE ITERATATION IN A CONTEXT 
;;; - level 1 
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-no-contradiction-found-in-context-level1
	(declare (salience ?*level1-no-contrad-found-in-context-salience*))
	;;; after all the resolution rules have been applied in the context
	(context (name ?cont&~0) (depth ?depth&1) (parent ?par) (generating-cand ?gen-cand))
=>
	(if (or ?*print-actions* ?*print-hypothesis*) then
		(printout t "NO CONTRADICTION FOUND IN CONTEXT " ?cont "." crlf)
	)
	;;; properly destroy the present context so as not to saturate memory
	(assert (clean-and-retract ?cont))
)


(defrule TE2-start-TE-level1
	(declare (salience ?*level1-generate-context-salience*))
    ;;; level 1
    (technique 0 BRT)
=>
    (assert (technique 0 TE2))
	(assert (phase 0 1))
	(printout t crlf "*** STARTING T&E IN CONTEXT " 0  " at depth " 1 " with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining ***" crlf)
	(if ?*print-phase* then
        (if ?*print-hypothesis* then (printout t crlf))
        (printout t "        STARTING PHASE 1 IN CONTEXT 0 with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining" crlf)
        (if ?*print-hypothesis* then (printout t crlf))
    )
)



(defrule TE2-generate-context-level1
	(declare (salience ?*level1-generate-context-salience*))
    (technique 0 TE2)
    ;;; level 1
	(context (name ?par&0) (depth ?depth&0))
    ;;; only one context other than 0 with parent 0 is considered at a time:
	(not (context (name ?cont&~?par) (parent ?par)))
	;;; each remaining cand of ?par will be re-tried in each phase, but not re-tried in the same phase
	(phase ?par ?ph) 
    ?gen <- (candidate (context ?par) (status cand) (label ?gen-cand))
	(not (TE2-tried ?par ?ph ?gen-cand))
    ;;; try only the candidates that are restricted targets for T&E
    (or (test (not ?*restrict-TE-targets*))
        (is-restricted-TE-target 0 ?gen-cand)
    )

    ;;; if the focus list is not empty, the following condition restricts the search to the candidates in it
    ;;; t-whips should not be used if the focus list is not empty (this would restrict them improperly)
    (or (not (candidate-in-focus (context ?par))) (candidate-in-focus (context ?par) (label ?gen-cand)))
=>
	;;; choose ?gen-cand as a hypothesis	
	(bind ?*context-counter* (+ ?*context-counter* 1))
	(bind ?depth1 (+ 1 ?depth))
	(if (or ?*print-actions* ?*print-hypothesis*) then 
		(printout t crlf "GENERATING CONTEXT " ?*context-counter* " AT DEPTH " ?depth1 ", SON OF CONTEXT " ?par ", FROM HYPOTHESIS " ?gen-cand "." crlf)
	)
	;;; assert the new context
	(assert (context (name ?*context-counter*) (parent ?par) (depth ?depth1) (generating-cand ?gen-cand)))
	(assert (technique ?*context-counter* BRT))
	;;; assert the generating value of the new context, with flag 0
	(duplicate ?gen (context ?*context-counter*) (status c-value) (flag 0))
	;;; remember that ?gen-cand was tried in ?par at phase ?ph
	(assert (TE2-tried ?par ?ph ?gen-cand))
)



(defrule TE2-iterate-phase-level1
	(declare (salience ?*level1-iterate-phase-salience*))
    (technique 0 TE2)
	;;; if the grid is not solved by phase ?ph
	(context (name ?par&0) (depth ?depth&0))
	(csp-variable (name ?csp))
	(forall (is-csp-variable-for-label (csp-var ?csp) (label ?lab))
        (not (candidate (context ?par) (status c-value) (label ?lab)))
    )
	?phase <- (phase ?par ?ph)
	;;; and if phase ?ph was productive
	?ce <- (phase-productive-in-context ?par ?ph)
	
=>
	(if ?*print-phase* then
        (if ?*print-hypothesis* then (printout t crlf))
        (printout t "        STARTING PHASE " (+ ?ph 1) " IN CONTEXT 0 with " ?*nb-csp-variables-solved* " csp-variables solved and " ?*nb-candidates* " candidates remaining" crlf)
        (if ?*print-hypothesis* then (printout t crlf))
    )
	(assert (phase ?par (+ ?ph 1)))
	(retract ?ce)
	(retract ?phase)
)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; CLEAN WHAT'S LEFT BY T&E
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule TE2-clean-1
    (declare (salience ?*TE-clean-salience*))
    ?t <- (technique 0 TE2)
=>
    (retract ?t)
)


(defrule TE2-clean-2
    (declare (salience ?*TE-clean-salience*))
    (not (technique 0 TE2))
    ?ph <- (phase ?x ?y)
=>
    (retract ?ph)
)


(defrule TE2-clean-3
    (declare (salience ?*TE-clean-salience*))
    (not (technique 0 TE2))
    (not (phase ? ?))
    ?b <- (TE1-tried ?par&0 ?ph ?gen-cand)
=>
    (retract ?b)
)









